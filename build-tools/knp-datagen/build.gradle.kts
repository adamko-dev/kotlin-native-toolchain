import org.gradle.kotlin.dsl.invoke
import org.gradle.kotlin.dsl.support.serviceOf
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
  id("conventions.kotlin-gradle-plugin")
  kotlin("plugin.serialization") version embeddedKotlinVersion
}

description = "Determine the dependencies required for a Kotlin/Native Konan installation."

dependencies {
  implementation(platform(libs.kotlinxSerialization.bom))
  implementation(libs.kotlinxSerialization.json)

  compileOnly("org.jetbrains.kotlin:kotlin-gradle-plugin:${embeddedKotlinVersion}")

  testImplementation(platform(libs.junit.bom))
  testImplementation(libs.junit.jupiter)
  testRuntimeOnly(libs.junit.platformLauncher)

  testImplementation(libs.kotest.assertions)
}

kotlin {
  compilerOptions {
    jvmTarget = JvmTarget.JVM_21 // required for HttpClient.use {}
    optIn.add("kotlinx.serialization.ExperimentalSerializationApi")
  }
}

val createBuildConstants by tasks.registering {
  group = project.name

  val fs = serviceOf<FileSystemOperations>()

  val kotlinVersion = libs.versions.kotlin
  inputs.property("kotlinVersion", kotlinVersion)

  val outputDir = layout.buildDirectory.dir("generated/source/kotlin")
  outputs.dir(outputDir).withPropertyName("outputDir")

  val projectName = project.name

  doLast {
    val outputSrcDir = outputDir.get().asFile
    fs.delete { delete(outputSrcDir) }
    outputSrcDir.mkdirs()

    outputSrcDir.resolve("BuildConstants.kt").writeText(
      """
      |// This file is autogenerated by the $projectName build script. Do not edit manually.
      |
      |@file:Suppress("ConstPropertyName")
      |
      |package dev.adamko.kntoolchain.tools.internal
      |
      |internal object BuildConstants {
      |  const val kotlinVersion = "${kotlinVersion.get()}"
      |}
      |""".trimMargin()
    )
  }
}

kotlin.sourceSets.main {
  kotlin.srcDir(createBuildConstants)
}

gradlePlugin {
  plugins.register("knp-datagen") {
    id = "dev.adamko.kntoolchain.tools.konan-dependencies-generator"
    implementationClass = "dev.adamko.kntoolchain.tools.KonanDependenciesDataFetcherPlugin"
  }
}
